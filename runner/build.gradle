plugins {
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version"
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'application'
    id 'maven-publish'
}
apply plugin: "org.jetbrains.kotlin.jvm"

group 'io.engenious.sift'
version '0.1-SNAPSHOT'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.3"
    implementation project(path: ':tongs-runner')
    implementation project(path: ':tongs-common')
    implementation project(path: ':tongs-plugin-api')
    implementation project(path: ':tongs-plugin-android')
//    implementation("com.github.engeniousio.tongs:tongs-runner") {
//        version {
//            strictly("27ab04d4e6ca24c776618eb6153e50f4c60b5dbb")
//        }
//    }
//    implementation("com.github.engeniousio.tongs:tongs-common") {
//        version {
//            strictly("27ab04d4e6ca24c776618eb6153e50f4c60b5dbb")
//        }
//    }
//    implementation("com.github.engeniousio.tongs:tongs-plugin-api") {
//        version {
//            strictly("27ab04d4e6ca24c776618eb6153e50f4c60b5dbb")
//        }
//    }
//    implementation("com.github.engeniousio.tongs:tongs-plugin-android") {
//        version {
//            strictly("27ab04d4e6ca24c776618eb6153e50f4c60b5dbb")
//        }
//    }
    //noinspection GradleDependency // greater version from the above plugin is actually used
    implementation("com.android.tools.ddms:ddmlib:22.0")

    implementation "org.apache.sshd:sshd-core:${sshdVersion}"
    implementation "org.apache.sshd:sshd-sftp:${sshdVersion}"

    implementation "com.github.ajalt.clikt:clikt:3.0.1"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-core:1.1.0"
    implementation "org.http4k:http4k-client-okhttp:${http4kVersion}"
    implementation "org.http4k:http4k-server-ktorcio:${http4kVersion}"
    implementation "org.http4k:http4k-core:${http4kVersion}"
    implementation "org.http4k:http4k-format-kotlinx-serialization:${http4kVersion}"
    implementation "org.http4k:http4k-jsonrpc:${http4kVersion}"
    implementation "org.http4k:http4k-multipart:${http4kVersion}"
    implementation "io.insert-koin:koin-core:2.1.6" // should be the same version as in upstream
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.0" // should be the same version as OkHttpClient
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation 'org.slf4j:slf4j-log4j12:1.7.30'
    implementation 'junit:junit:4.12'

    modules {
        module("org.koin:koin-core") {
            replacedBy("io.insert-koin:koin-core", 'group is changed')
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
    kotlinOptions.freeCompilerArgs = ["-Xopt-in=kotlin.RequiresOptIn", "-Xinline-classes"]
}
compileTestKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
    kotlinOptions.freeCompilerArgs = ["-Xopt-in=kotlin.RequiresOptIn"]
}

application {
    applicationName = 'sift'
    mainClassName = "io.engenious.sift.MainCliKt"
}
runShadow {
    workingDir = project.getProperties().getOrDefault("workingDir", project.projectDir)
}

installShadowDist.destinationDir = file(installShadowDist.destinationDir.path.replaceFirst('-shadow$', ''))

tasks.named('installDist').configure({
    dependsOn(installShadowDist)
    enabled = false
})

publishing {
    publications {
        //noinspection GroovyAssignabilityCheck
        maven(MavenPublication) {
            from components.java
            groupId = rootProject.group
        }
    }
}