/*
 * Copyright 2019 TarCV
 * Copyright 2015 Shazam Entertainment Limited
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the License.
 *
 * You may obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific language governing permissions and limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName = 'com.github.tarcv.tongs.TongsCli'

java {
    //noinspection GroovyUnusedAssignment
    sourceCompatibility = javaSourceCompatibility
    //noinspection GroovyUnusedAssignment
    targetCompatibility = javaTargetCompatibility
}

dependencies {
    api project(':tongs-common')
    api project(':tongs-plugin-api')
    implementation project(':tongs-plugin-android')

    implementation deps.koin
    implementation deps.guava
    implementation deps.commonsIo
    implementation deps.commonsLang
    implementation deps.jsr305
    implementation deps.handleBars
    implementation deps.handleBarsHuminize
    implementation deps.jcommander
    implementation deps.slf4j
    implementation deps.gson
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation("org.lesscss:lesscss:1.7.0.1.1") {
        exclude(module: "slf4j-simple")
    }

    testImplementation project(':tongs-common-test')
    testImplementation deps.hamcrest
    testImplementation(deps.junit) {
        exclude module:'hamcrest-core'
    }
    testImplementation(
            deps.jmock,
            deps.jmockJUnit)
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
}

jar {
    manifest {
        attributes 'Implementation-Title': POM_NAME,
                'Implementation-Version': VERSION_NAME,
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion,
                'Main-class': mainClassName
    }
//    archiveBaseName = POM_ARTIFACT_ID
}

run {
    systemProperties System.getProperties()

    if(project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
    if (project.hasProperty('workingDir')) {
        workingDir file(project.workingDir)
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }
}
